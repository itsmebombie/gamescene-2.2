extract obj_props

extract { // remove this once obj_props gets the 2.2 update
    HIDE_P1: @object_key::{id: 162, pattern: @number, name: "HIDE_P1"},
    HIDE_P2: @object_key::{id: 163, pattern: @number, name: "HIDE_P2"},
    NO_DEATH_SFX: @object_key::{id: 576, pattern: @number, name: "NO_DEATH_SFX"},
    EVENT_EXTRA_ID: @object_key::{id: 447, pattern: @number, name: "EVENT_EXTRA_ID"},
    EVENT_EXTRA_ID2: @object_key::{id: 525, pattern: @number, name: "EVENT_EXTRA_ID2"},
    EVENT_LIST: @object_key::{id: 430, pattern: @array|@string|@number, name: "EVENT_LIST"},
    FREE_MODE: @object_key::{id: 111, pattern: @number, name:"FREE_MODE"}, // NEW
}

// convert to p1/p2
p1 = e => e.with(EVENT_EXTRA_ID2, 1)
p2 = e => e.with(EVENT_EXTRA_ID2, 2)

// convert to start_group if its a function
t = t => t.start_group if t.type == @trigger_function else t

ev = obj{ // event obj
    OBJ_ID: 3604,
    X: -100, Y: 0,
}

ev_down = ev.with(EVENT_LIST, 69)
ev_up = ev.with(EVENT_LIST, 70)

ev_ldown = ev.with(EVENT_LIST, 71)
ev_lup = ev.with(EVENT_LIST, 72)
ev_rdown = ev.with(EVENT_LIST, 73)
ev_rup = ev.with(EVENT_LIST, 74)

// pattern
p = @trigger_function|@group

// add
add = (b,g) => b.with(TARGET, t(g)).add()

return {
    // g = group or trigger function, p = pattern
    button: (g: p) => add(ev_down, g),
    button_end: (g: p) => add(ev_up, g),

    button_a: (g: p) => add(p1(ev_down), g),
    button_b: (g: p) => add(p2(ev_down), g),
    button_a_end: (g: p) => add(p1(ev_up), g),
    button_b_end: (g: p) => add(p2(ev_up), g),
    
    button_a_left: (g: p) => add(p1(ev_ldown), g),
    button_a_right: (g: p) => add(p1(ev_rdown), g),
    button_b_left: (g: p) => add(p2(ev_ldown), g),
    button_b_right: (g: p) => add(p2(ev_rdown), g),
    button_a_left_end: (g: p) => add(p1(ev_lup), g),
    button_a_right_end: (g: p) => add(p1(ev_rup), g),
    button_b_left_end: (g: p) => add(p2(ev_lup), g),
    button_b_right_end: (g: p) => add(p2(ev_rup), g),

    init: (hide_players: @bool = true, disable_death_sfx: @bool = true, lock_players: @bool = true) {
        plr = hide_players as @number
        $.add(obj{ // options
            OBJ_ID: 2899,
            X: -30, Y: 0,
            HIDE_P1: plr, HIDE_P2: plr,
            NO_DEATH_SFX: disable_death_sfx as @number,
        })

        hide = ?g

        $.add(obj{ // dual mode as 2.2 (free cam)
            OBJ_ID: 286,
            X: 0, Y: 15,
            GROUPS: hide,
            FREE_MODE: 1
        })

        $.add(obj{ // gravitation
            OBJ_ID: 10,
            X: 0, Y: 15,
            GROUPS: hide,
        })

        if lock_players { // lock players movement
            $.add(obj{
                OBJ_ID: 1,
                X: -30, Y:15,
                GROUPS: hide
            })

            $.add(obj{
                OBJ_ID: 1,
                X: 30, Y:15,
                GROUPS: hide
            })

            $.add(obj{
                OBJ_ID: 1,
                X: 0, Y:-15,
                GROUPS: hide
            })

            $.add(obj{
                OBJ_ID: 1,
                X: 0, Y:45,
                GROUPS: hide
            })
        }

        hide.alpha(0)
    },
}